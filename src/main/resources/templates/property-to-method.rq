prefix idsv: <http://industrialdataspace.org/2016/10/idsv/core#>
prefix valid: <http://industrialdataspace.org/2017/01/idsv/validation#>

template idsv:property-to-method(?property, ?class, ?syntax) {

    "\n\t"

    # getter method

        # visibility modifier
        if (str(?syntax) = 'class', 'public ', '')

        # return type and annotation
        if (bound(?constraint),
            st:call-template(idsv:propertyAnnotation, ?constraint, ?range),
            st:call-template(idsv:range, ?range))

        " "

        # method name
        "get" ucase(substr(str(?label), 1, 1)) substr(str(?label), 2) "()"

    if (str(?syntax) = 'class', st:call-template(idsv:getter-method-body, ?property), ';')
    "\n\t"

    # setter method

        # visibility modifier
        if (str(?syntax) = 'class', 'public ', '')

        # method name
        "void set"
        ucase(substr(str(?label), 1, 1)) substr(str(?label), 2)

        "("
            # parameter type and annotation
            if (bound(?constraint),
                st:call-template(idsv:propertyAnnotation, ?constraint, ?range),
                st:call-template(idsv:range, ?range))

            " "

            # parameter label
            str(?label)
        ")"

    if (str(?syntax) = 'class', st:call-template(idsv:setter-method-body, ?property), ';')
    "\n\t"

}
where  {
   ?property rdfs:label ?label;
        rdfs:range ?range.

    OPTIONAL {
        ?class idsv:validation ?validation.
        ?validation owl:onProperty ?property.
        ?validation valid:constraint ?constraint.
    }

} 
