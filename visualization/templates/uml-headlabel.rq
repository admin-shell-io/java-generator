prefix idstt: <https://w3id.org/idsa/transformationtemplates/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix sh: <http://www.w3.org/ns/shacl#>

# Creates the cardinality descriptions for a property of a given class which points to another class within the ontology namespace.

template idstt:uml-headlabel(?class, ?property) {

    if (
        # check for the first number if min cardinality exists:
        !(bound(?minCount)),
        # no minimal cardinality
        if (
            # check if max cardinality exists
            !(bound(?maxCount)),
            # no min and no max cardinality:
            " \"*\" ",
            # no min but a max cardinality:
            concat(" \"0..", concat(?maxCount, "\" "))
        ),
        # min cardinality exists
        if (
            # check if max cardinality exists
            !(bound(?maxCount)),
            # min cardinality exists and no max cardinality:
            concat(" \"", concat(?minCount, "..*\" ")),
            # min and a max cardinality:
            if (?minCount = ?maxCount,
                # min = max -> only one cardinality needed!
                concat(" \"", concat(?minCount, "\" ")),
                # else: print both
                concat(" \"", concat(?minCount, concat("..", concat(?maxCount, "\" "))))
            )
        )
    )
    
}
where {
    ?property rdfs:range ?range.
    {?property rdfs:domain/^(rdfs:subClassOf*) ?class.} UNION {
    ?property rdfs:domain/owl:unionOf/rdf:rest*/rdf:first/^(rdfs:subClassOf*) ?class.}

    OPTIONAL {
        ?class rdfs:subClassOf* ?superClass .
        ?classShape sh:targetClass ?superClass ;
            (sh:xone|sh:or)*/rdf:rest*/rdf:first*/sh:property ?propertyShape .
        ?propertyShape sh:path ?property ;
          sh:minCount ?minCount ;
          sh:maxCount ?maxCount .
    }

    OPTIONAL {
        ?class rdfs:subClassOf* ?superClass .
        ?classShape sh:targetClass ?superClass ;
            (sh:xone|sh:or)*/rdf:rest*/rdf:first*/sh:property ?propertyShape .
        ?propertyShape sh:path ?property ;
        sh:minCount ?minCount .
    }

    OPTIONAL {
        ?class rdfs:subClassOf* ?superClass .
        ?classShape sh:targetClass ?superClass ;
            (sh:xone|sh:or)*/rdf:rest*/rdf:first*/sh:property ?propertyShape .
        ?propertyShape sh:path ?property ;
        sh:maxCount ?maxCount .
    }
}

