#
# Copyright (c) 2021 Fraunhofer-Gesellschaft zur Foerderung der angewandten Forschung e. V.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

prefix idstt: <https://w3id.org/idsa/transformationtemplates/>
prefix sh: <http://www.w3.org/ns/shacl#>

template idstt:get-class-hash-code-property-list(?class) {

    group {

        IF( (BOUND(?isPrimitiveArray) && ?isPrimitiveArray),
            #then
            "Arrays.hashCode(",
            #else
            ""
        )
        format {
            "this.%s"
            ?field
        }
        IF( (BOUND(?isPrimitiveArray) && ?isPrimitiveArray),
            #then
            ")",
            #else
            ""
        )
        ; separator = ",\n\t\t\t"
    }
    
}
WHERE  {
    ?class rdfs:subClassOf* ?superClass .
    OPTIONAL {
        {
            ?property rdfs:domain ?superClass .
            BIND(st:call-template(idstt:to_java_field_name, ?property, ?superClass) as ?field )
        }
        UNION
        {   
            ?property rdfs:domain/owl:unionOf/rdf:rest*/rdf:first ?superClass .
            BIND(st:call-template(idstt:to_java_field_name, ?property, ?superClass) as ?field )
        }

        #Note: This block of code below was taken from the "range-to-java-type" template
        OPTIONAL {
            ?property rdfs:range ?range.
            {?property rdfs:domain/^(rdfs:subClassOf*) ?class.} UNION {
            ?property rdfs:domain/owl:unionOf/rdf:rest*/rdf:first/^(rdfs:subClassOf*) ?class.}

            OPTIONAL {
                ?class rdfs:subClassOf* ?superClass .
                ?classShape sh:targetClass ?superClass ;
                    (sh:xone|sh:or)*/rdf:rest*/rdf:first*/sh:property ?propertyShape .
                ?propertyShape sh:path ?property ;
                sh:maxCount ?maxCount .
            }

            BIND(
                if ( BOUND(?maxCount),
                    # -> [?, max]
                    if ( ?maxCount > 1,
                        # then -> multiple values allowed
                        true,
                        # else -> at most 1 value allowed
                        false
                    ),
                    # no max cardinality bound [?, *]
                    true
                ) as ?isArray
            )

            #Does the java type contain "[]"? If so, it is a primitive array and needs Arrays.hashCode
            BIND(regex(st:call-template(idstt:range-to-basic-type, ?property, ?range, ?class, ?isArray), "\\[\\]") AS ?isPrimitiveArray)
        }
    }
}