#
# Copyright (c) 2021 Fraunhofer-Gesellschaft zur Foerderung der angewandten Forschung e. V.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

prefix idstt: <https://w3id.org/idsa/transformationtemplates/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix sh: <http://www.w3.org/ns/shacl#>

template idstt:range-to-java-type(?class, ?property, ?range, ?bracketOpen, ?bracketClose) {

    if ( ?isArray,
        # then
        concat(
            concat(
				#For this one specific property, we want to use Collection instead of List. Writing it in a format in which further properties can be added easily.
				if(EXISTS { VALUES ?property { <https://admin-shell.io/aas/3/0/RC01/SubmodelElementCollection/value> } } , "Collection", "List"),
				str(?bracketOpen)),
            concat(st:call-template(idstt:range-to-basic-type, ?property, ?range, ?class, ?isArray), str(?bracketClose))
        ),
        # else
        st:call-template(idstt:range-to-basic-type, ?property, ?range, ?class, ?isArray)
    )

}
where {
    ?property rdfs:range ?range.
    {?property rdfs:domain/^(rdfs:subClassOf*) ?class.} UNION {
    ?property rdfs:domain/owl:unionOf/rdf:rest*/rdf:first/^(rdfs:subClassOf*) ?class.}

    OPTIONAL {
        ?class rdfs:subClassOf* ?superClass .
        ?classShape sh:targetClass ?superClass ;
            (sh:xone|sh:or)*/rdf:rest*/rdf:first*/sh:property ?propertyShape .
        ?propertyShape sh:path ?property ;
        sh:maxCount ?maxCount .
    }

    BIND(
        if ( BOUND(?maxCount),
            # -> [?, max]
            if ( ?maxCount > 1,
                # then -> multiple values allowed
                true,
                # else -> at most 1 value allowed
                false
            ),
            # no max cardinality bound [?, *]
            true
        ) as ?isArray
    )

} LIMIT 1
